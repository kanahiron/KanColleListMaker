/*
* SoftWareName(Jp)	: 艦これ一覧めいかー
* SoftWareName(En)	: KanColleListMaker
* Version			: 2.6
* Author			: kanahiron
* License			: MIT
* Copyright(C) 2014 kanahiron
*/

#uselib "shlwapi.dll"
#cfunc PathIsDirectory "PathIsDirectoryA" sptr

#uselib "shell32.dll"
#func global DragAcceptFiles "DragAcceptFiles" int,int
#func global DragQueryFile   "DragQueryFileA"  int,int,int,int
#func global DragQueryPoint  "DragQueryPoint"  int,int
#func global DragFinish      "DragFinish"      int

#uselib "gdi32.dll"
#cfunc global CreateDC "CreateDCA" sptr,sptr,sptr,int
#func global DeleteDC "DeleteDC"  int
#func global GetDC "GetDC" int
#func global ReleaseDC "ReleaseDC" int,int
#func global BitBlt "BitBlt" int,int,int,int,int,int,int,int,int
#func global CreateDIBSection "CreateDIBSection" int,int,int,int,int,int
#func global CreateCompatibleBitmap "CreateCompatibleBitmap" int,int,int
#func global SelectObject "SelectObject" int,int
#func global DeleteObject "DeleteObject" int 

#uselib "user32.dll"
#func global MoveWindow "MoveWindow" int , int , int , int , int , int
#func global GetWindowLong "GetWindowLongA" int,int
#func global SetWindowLong "SetWindowLongA" int,int,int
#func global SetLayeredWindowAttributes "SetLayeredWindowAttributes" int,sptr,int,int
#func global GetCursorPos "GetCursorPos" int
#func global SetCursorPos "SetCursorPos" int,int
#func global ScreenToClient "ScreenToClient" int ,int
#func global ChildWindowFromPoint "ChildWindowFromPoint" int ,int
#func global SetFocus "SetFocus" int
#func global SetParent "SetParent" int ,int
#func global RegisterHotKey "RegisterHotKey" int,int,int,int
#func global UnregisterHotKey "UnregisterHotKey" int,int
#cfunc global IsWindowVisible "IsWindowVisible" int
#cfunc global GetSystemMetrics "GetSystemMetrics" int
#func global SetClassLong "SetClassLongA" int,int,int
#func global LoadCursor "LoadCursorA" int,int
#func global SetTimer "SetTimer" int,int,int,sptr
#func global KillTimer "KillTimer" int,int

#uselib "kernel32.dll"
#func MultiByteToWideChar "MultiByteToWideChar" int,int,int,int,int,int

#uselib "winmm.dll"
#cfunc global timeGetTime "timeGetTime"

#include "kmodule.as"
#include "ImageFileModule2.as"
#include "inimodule.as"
#include "exdialog.as"
#include "TsubuyakiSoup_mod.hsp"
#include "key.hsp"
#include "jsonParse.as"

#include "makelistmodule_Ver6_4.as"

#pack "data2.png"
#packopt name "艦これ一覧めいかー Ver2_6"
#packopt hide 1
#packopt orgpath 1

#define CELLWMAX 8
#define CELLHMAX 7
#define HOTKEYID	7789
#define timerID		100
#define klmver 26000
#define WM_LBUTTONDOWN	0x0201
#define ctype round(%1) int(strf("%%0.0f", %1))
#define SOFTNAME "艦これ一覧めいかー Ver2.6"

#define nidPush nidindex++:nidstack(nidindex) = ginfo(3)
#define nidPop gsel nidstack(nidindex),0:nidindex--

	if tCupVer != "2.1":end

	onexit gosub *en_d

	mode = 0
	listmode = 0
	yubiwa = 1
	kantai = 1
	autosearch = 1
	hidename = 0
	sti = 0
	cliflag = 0
	cellmode = 0
	mesy = 30
	hotkeycap = 0
	ret = 0

	jpgquality = 90
	namecut = 0
	hidelog = 0
	hidelog_ = hidelog
	jpgsave = 0
	rapidshooting = 0
	frontrow = 0
	othergame = 0
	
	availablecap = 0
	availableyabumi = 0
	hotkeyset = 0
	saveListName = ""
	
	tempint = 0
	tempstr = ""
	currentdir = dir_cur

	modeNum = 3

	dim data,CELLWMAX,CELLHMAX,modeNum
	dim bc,(CELLWMAX*CELLHMAX*modeNum)
	dim dd,4,modeNum
	dim mesxy,modeNum
	dim maxscrwh,modeNum
	dim mindexxy,modeNum
	dim mxy,2
	dim mxy_,2
	dim dmxy,2
	dim sscap,4
	dim sscapwh,2
	dim cellnum,2,modeNum
	dim scrsize,2,modeNum
	dim cellsize,2,modeNum
	dim tempmouse,2
	dim disinfo,4
	dim scrsizetemp,2,modeNum
	dim nidstack,100
	nidindex = 0
	
	ddim AspectRatio,modeNum
	sdim sscapmes,256
	sdim logmessage,256
	sdim modemes,64,4
	sdim stemp
	sdim sssavedir ,260
	sdim listsavedir ,260
	sdim yabumidir,260
	sdim savekind,64,modeNum
	sdim hotkeycapchar
	sdim hotkeystr
	sdim hotkeystr_temp
	sdim savelogtext,1024
	sdim picStack,1024,4
	sdim tweettext,200
	sdim picHistory,512,101
	cellnum(0,0) = 6,4
	cellnum(0,1) = 5,5
	cellnum(0,2) = 6,4

	twiPicFrameXL = 185,250,315,380,184
	twiPicFrameXR = 248,313,378,443,444
	twiPicFrameYU = 3 ,3 , 3, 3, 45
	twiPicFrameYD = 42,42,42,42,209
	twiOrder = 0,0,0,0
	twiOrdercount = 0
	
	enableFirst = 1
	enableSecond = 1
	enableThread = 0
	enableFourth = 0

	directLink = 1
	airStation = 0
	
	bc(0) = 0
	maxscrwh(0) = int(0.8*ginfo(20)),int(0.8*ginfo(21))
	mindexxy(0) = 0,0
	modemes(0) = "SSキャプチャ","艦娘一覧","攻略編成","その他一覧作成モード","オプション"
	mxy(0) = 0,0
	mxy_(0) = 0,0
	sscapwh = 0,0
	seccapf = 0
	sscapmes = "取得する"
	savekind = ".png",".jpg"
	seqcapmes = "開始"
	luposx = 0
	luposy = 0
	rdposx = 0
	rdposy = 0
	twijpg = 1
	timersec = 5
	dropwindowid = 0
	dropf = 0
	intervalsavedir = ""
	savenamepath = ""
	intervalShotDir = ""
	
	fontname = "ＭＳ　ゴシック"
	fontnum = "ＭＳ　ゴシック"
	fontsizename = 34
	fontsizenum = 34
	fontColorFirstFleetStr = "FF0000"
	fontColorSecondFleetStr = "0000FF"
	fontColorThreadFleetStr = "20AC4C"
	fontColorFourthFleetStr = "E06020"
	
	fontColorFirstFleet = int("$"+fontcolorFirstFleetstr)
	fontColorSecondFleet = int("$"+fontcolorSecondFleetstr)
	fontColorThreadFleet = int("$"+fontcolorThreadFleetstr)
	fontColorFourthFleet = int("$"+fontcolorFourthFleetstr)
	
	FirstFleetName = "第一艦隊"
	SecondFleetName = "第二艦隊"
	ThreadFleetName = "ボス支援"
	FourthFleetName = "道中支援"
	
	picHistoryNum = 0
	mainWindowSmall = 0
	
	windowsSizeChangeBMes = "↑","↓"
	autoSearchFailureCount = 0
	
	userdata = ""
	ACCESS_TOKEN = ""
	ACCESS_SECRET = ""
	
	tCupInit "KanColleListMaker", CONSUMER_KEY, CONSUMER_SECRET, 7
	
	scrsize(0,0) = limit(1358,210,round(0.4*maxscrwh(0))),scrsize(0,0)*aspectRatio(0)
	scrsize(0,1) = limit(1120,250,round(0.4*maxscrwh(0))),scrsize(0,1)*aspectRatio(1)
	scrsize(0,2) = limit(1120,250,round(0.4*maxscrwh(0))),scrsize(0,2)*aspectRatio(2)
	scrsizetemp(0,0)  = scrsize(0,0),scrsize(1,0)
	scrsizetemp(0,1)  = scrsize(0,1),scrsize(1,1)
	scrsizetemp(0,2)  = scrsize(0,2),scrsize(1,2)
	
	gosub *iniload
	gosub *dataCheck
	gosub *disinfoinit
	gosub *loadHistory
	
	gosub *drawString
	
	buffer 16,640,150
	syscolor 16
	font "メイリオ",25,1
	mes "スクリーンショットをここにドラッグアンドドロップ"
	mes "またはホットキー(Altキー＋Zキー同時押し)で
	mes "                                         画像を追加してください"
	mes "セルの画像はウィンドウ外にドラッグで消去できます"
	
	screen 19,448,250,2
	oncmd gosub *twiwindowimage,0x0201
	oncmd gosub *rideCursor,WM_MOUSEMOVE
	oncmd gosub *getText ,WM_COMMAND
	oncmd gosub *OnDropFiles, WM_DROPFILES
	hwnd19 = hwnd
	
	;screen 10,disinfo(2),disinfo(3)
	buffer 10,disinfo(2),disinfo(3)
	
	//手動選択重ね用
	bgscr 11,disinfo(2),disinfo(3),2
	hwnd11 = hwnd
	font "メイリオ",30

	//範囲選択用レイヤードウィンドウ
	bgscr 9,ginfo(20),ginfo(21),2
	hwnd9 = hwnd
	color 0,255,0
	boxf
	GetWindowLong hwnd9,-20
	SetWindowLong hwnd9,-20,stat|$80000
	SetLayeredWindowAttributes hwnd9,0x00000000,128,LWA_ALPHA

	screen 1,maxscrwh(0),maxscrwh(1),2:title "表示させたいセルにD&D"
	hwnd1 = hwnd
	DragAcceptFiles hwnd1, 0
	oncmd gosub *OnDropFiles, WM_DROPFILES
	oncmd gosub *windowsize, WM_SIZING
	oncmd gosub *listdrag, WM_LBUTTONDOWN
	framesx = ginfo_sizex - ginfo_winx
	framesy = ginfo_sizey - ginfo_winy
	kmmouse_init
	
	//一覧モード出力用
	buffer 2,disinfo(2),disinfo(3)

	//画像読み込み用
	buffer 7
	picload "data2.png"
	
	;//キャプチャ作成用
	buffer 8
	//キャプ用
	buffer 5,disinfo(2),disinfo(3)

	//背景コピー用
	buffer 4,maxscrwh(0),maxscrwh(1)
	//縮小処理用
	buffer 3,maxscrwh(0)/5,maxscrwh(1)/3
	
	screen 0,350,700,2:title SOFTNAME
	hwnd0 = hwnd
	DragAcceptFiles hwnd0, 0
	oncmd gosub *OnDropFiles ,WM_DROPFILES
	oncmd gosub *setmode ,WM_COMMAND
	oncmd gosub *hotkey ,WM_HOTKEY
	oncmd gosub *sscaptcha,0x0113
	gsel listsize1
	syscolor 15:boxf:color
	font "メイリオ",13
	objmode 2
	
	objsize 175,30
	pos 0,0
	combox cellmode,30,"  SSキャプチャモード\n  艦娘一覧モード\n  攻略編成モード\n  その他一覧作成モード\n  オプション"
	;combox cellmode,30,"  SSキャプチャモード\n  艦娘一覧モード\n  攻略編成モード\n  オプション"
	comboxid = stat
	hcombox = objinfo_hwnd(comboxid)
	modeOptionNum = 4
	objsize 0,0
	button "dum_my",*adummy
	
	gosub *ssmode
	gosub *hotkeycapsetting
	
	gsel 0,1+frontrow

	if autosearch {
		gsel 0,-1
		wait 50
		gosub *sssetting
		gsel 0,1+frontrow
	}

	gosub *twiinit

	if enabletweetwindow {
		gsel 19,1
		gsel 0,1+frontrow
	}

	gosub *VerCheck
	stop

*adummy
	
*setmode
	oncmd 0

	if (lparam = hcombox) & (((wparam>>16) & 0x0000FFFF) = 1){
	
		if (mode != 0) & (mode != modeOptionNum) {
			DragAcceptFiles hwnd1, 0
			DragAcceptFiles hwnd0, 0
			gsel 1,0
		}
	
		if mode = modeOptionNum{
			gosub *dataCheck			
			gosub *hotkeycapsetting
			gosub *disinfoinit
			gosub *twiinit
		}
	
		sendmsg hCombox, $147
		mode = stat
	
		if (mode != 0) & (mode != modeOptionNum) {
			DragAcceptFiles hwnd1, 1
			DragAcceptFiles hwnd0, 1
		}
	
		if mode = 0{
			gsel 0
			gosub *ssmode
		}
		if mode = 1{
			listmode = 0
			gsel 1,0
			gosub *list
		}
		if mode = 2{
			listmode = 1
			gsel 1,0
			gosub *list
		}
		if mode = 3{
			listmode = 2
			gsel 1,0
			gosub *list
		}
		if mode = modeOptionNum{
			gosub *option
		}
		
	} else {
	
		if mode != modeOptionNum{

			if (lparam = enabletweetwindowch){
				sendmsg enabletweetwindowch, $F2
				enabletweetwindow = stat & 0x01
				if enabletweetwindow {
					gsel 19,1+frontrow
				} else {
					gsel 19,-1
				}
				gsel 0
	
			}
		}

		if ((mode != 0)&(mode != modeOptionNum)){
			gosub *listchange
		}
	}
	
	
	oncmd 1
return

*listchange

	nidPush
	if mode = 1{
	
		gsel 0
		lvnamef = lvname
		
		w = 0
		h = 0
		capx = 0
		capy = 0
		cellratio = 0,0
		sendmsg yubiwach, $F2
		yubiwa = stat & 0x01
		sendmsg kantaich, $F2
		kantai = stat & 0x01
		sendmsg pagech, $F2
		page = stat & 0x01
		sendmsg lvnamech, $F2
		lvname = stat & 0x01
	
		if lvname = 1{
			objenable yubiwacid,0
			objenable kantaicid,0
			objenable pagecid,0
		} else {
			objenable yubiwacid,1
			objenable kantaicid,1
			objenable pagecid,1
		}
		
		if yubiwa = 0 & kantai = 0 : w = 382 : capx = 392
		if yubiwa = 1 & kantai = 1 : w = 440 : capx = 360
		if yubiwa = 1 & kantai = 0 : w = 408 : capx = 392
		if yubiwa = 0 & kantai = 1 : w = 415 : capx = 360
		if page = 0:h = 279
		if page = 1:h = 315
		if lvname = 1:w = 190: capx = 392:h = 279
		capy = 154
	
		dd(0,0) = capx,capy,w,h
		cellratio = 1.0*h/w

		if lvname = 0{
			if lvnamef = 1{
				scrsize(0,0) = scrsizetemp(0)
			}
			cellsize(0,0) = scrsize(0,0)/(cellnum(0,0)+1),round(cellratio*cellsize(0,0))
			scrsize(1,0) = round(cellsize(1,0)* (cellnum(1,0)+1) )
			
		} else {
			if lvnamef = 0{
				scrsizetemp(0,0)  = scrsize(0,0)
				cellsize(1,0) = scrsize(1,0)/(cellnum(1,0)+1)
				cellsize(0,0) = round(1.0*cellsize(0,0)/cellratio)
				scrsize(0,0) = round(cellsize(0,0)* (cellnum(0,0)+1) )
			}
		}
		AspectRatio(0,0) = 1.0*scrsize(1,0)/scrsize(0,0)
		gsel 1
		width scrsize(0,0),scrsize(1,0)
	}
	
	
	if mode = 2{
	
		sosuf = sosu
	
		w = 0
		h = 0
		capx = 0
		capy = 0
		cellratio = 0,0
		sendmsg sosukach, $F2
		sosuka = stat & 0x01
		sendmsg sosuch, $F2
		sosu = stat & 0x01
		sendmsg sokach, $F2
		soka = stat & 0x01
	
		sendmsg tuika1ch, $F2
		tuika1 = stat & 0x01
		sendmsg tuika2ch, $F2
		tuika2 = stat & 0x01
		sendmsg tuika3ch, $F2
		tuika3 = stat & 0x01
		sendmsg tuika4ch, $F2
		tuika4 = stat & 0x01
	
		sendmsg listsize1ch, $F2
		listsize1 = stat & 0x01
		sendmsg listsize2ch, $F2
		listsize2 = stat & 0x01
	
		sendmsg fleetPunctuationch, $F2
		fleetPunctuation = stat & 0x01
	
		if sosuka {
			w = 460
			h = 365
		}
		if sosu {
			w = 230
			h = 365	
		}
		if soka {
			w = 460
			h = 195
		}
		capx = 327
		capy = 103
	
		dd(0,1) = capx,capy,w,h
		cellratio = 1.0*h/w

		if sosuka | soka{
			if sosuf = 1{
				scrsize(0,1) = scrsizetemp(0,1)
			}
			cellsize(0,1) = scrsize(0,1)/(cellnum(0,1)+1),round(cellratio*cellsize(0,1))
			scrsize(1,1) = round(cellsize(1,1)* (cellnum(1,1)+1) )
			
		} else {
			if sosuf = 0{
				scrsizetemp(0,1)  = scrsize(0,1)
				cellsize(1,1) = scrsize(1,1)/(cellnum(1,1)+1)
				cellsize(0,1) = round(1.0*cellsize(1,1)/cellratio)
				scrsize(0,1) = round(cellsize(0,1) * (cellnum(0,1)+1) )
			}
		}

		AspectRatio(1) = 1.0*scrsize(1,1)/scrsize(0,1)
		gsel 1
		width scrsize(0,1),scrsize(1,1)
	}

	if (mode = 3){
		w = 0
		h = 0
		capx = 0
		capy = 0
		cellratio = 0,0
		sendmsg directLinkCh, $F2
		directLink = stat & 0x01
		sendmsg airStationCh, $F2
		airStation = stat & 0x01

		if directLink{
			w = 800
			h = 480
			capx = 0
			capy = 0
			cellnum(0,2) = 6,4
		}
		if airStation {
			w = 225
			h = 358
			capx = 575
			capy = 109
			cellnum(0,2) = 2,0
		}
		
		dd(0,2) = capx,capy,w,h
		cellratio = 1.0*h/w

		cellsize(0,2) = scrsize(0,2)/(cellnum(0,2)+1),round(cellratio*cellsize(0,2))
		scrsize(1,2) = round(cellsize(1,2)*(cellnum(1,2)+1) )
		AspectRatio(2) = 1.0*scrsize(1,2)/scrsize(0,2)
		gsel 1
		width scrsize(0,2),scrsize(1,2)
	}

	gosub *draw

	nidPop
return


//SSモード――――――――――――――――――――――――――――――――――
*ssmode
	
	gsel 1,-1
	gsel 0,1+frontrow
	clrobj 1
	syscolor 15:boxf:color

	mesy = 31
	
	pos 2,mesy
	mes "キャプチャ座標"
		
	objsize 80,25
	pos 95,mesy-3
	button gosub sscapmes,*sssetting
	sscapiid = stat
	mesy+=22
	
	if manualpos {
		if manualpos {
			manualposf = 0
		} else {
			manualposf = 1
		}
		pos 84+(manualposf*200),mesy+5
		mes "x"
		pos 0+(manualposf*200),mesy
		input luposx,40,23,5
		luposxiid = stat
		pos 40+(manualposf*200),mesy
		input luposy,40,23,5
		luposyiid = stat
		pos 95+(manualposf*200),mesy
		input rdposx,40,23,5
		rdposxiid = stat
		pos 135+(manualposf*200),mesy
		input rdposy,40,23,5
		rdposyiid = stat
	
		GetWindowLong objinfo(luposxiid,2), -16
		SetWindowLong objinfo(luposxiid,2), -16, stat | $2
		GetWindowLong objinfo(luposyiid,2), -16
		SetWindowLong objinfo(luposyiid,2), -16, stat | $2
		GetWindowLong objinfo(rdposxiid,2), -16
		SetWindowLong objinfo(rdposxiid,2), -16, stat | $2
		GetWindowLong objinfo(rdposyiid,2), -16
		SetWindowLong objinfo(rdposyiid,2), -16, stat | $2
		
		mesy+=23
	}
	
	if mainWindowSmall {
		mesaddx = 500
		tempmesy = mesy
	} else { 
		mesaddx=0
	}
	
	objsize 175,20
	pos 5+mesaddx,mesy
	chkbox "自動座標取得",autoSearch:mesy+=20
	autoSearchCId = stat
	pos 5+mesaddx,mesy
	chkbox "提督名と司令部Lvを隠す",hidename:mesy+=20
	hidenamebid = stat
	pos 5+mesaddx,mesy
	chkbox "ツイートウィンドウ表示",enabletweetwindow:mesy+=20
	enabletweetwindowcid = stat
	enabletweetwindowch = objinfo_hwnd(enabletweetwindowcid)
	
	if mainWindowSmall {
		mesy = tempmesy + 1
		syscolor 16
		line 3,mesy,172,mesy
		line 3,mesy+2,172,mesy+2
		mesy += 4
		color
	}

	objsize 130-(((yabumiautoupload = 1) & (availableyabumi = 1))*25),30
	pos 0,mesy
	button gosub "SSキャプチャ",*sscaptcha
	sscapbid = stat

	objsize 25,30
	pos 130-(((yabumiautoupload = 1) & (availableyabumi = 1))*25),mesy
	button gosub "開",*sssavefopen
	fopenbid = stat
	
	if ((yabumiautoupload = 1) & (availableyabumi = 1)) {
		pos 130,mesy
	} else {
		pos 200,mesy
	}	
	button gosub "UP",*yabumiupload
	yabumibid = stat
	
	pos 155,mesy
	objsize 20,30
	button gosub windowsSizeChangeBMes(mainWindowSmall),*windowsSizeChange
	windowsSizeChangebid = stat
	
	mesy+=30
	
	pos 2+(disableseqcap*-175),mesy+2
	mes "連続キャプチャ"
	objsize 80,25
	pos 95+(disableseqcap*-175),mesy
	if disableseqcap = 0:mesy+=24
	button gosub seqcapmes,*seqcap
	seqcapbid = stat
	
	dispcapy = mesy
	if dispcap {
		syscolor 16
		boxf 0,mesy,175,mesy+105
		syscolor 15
		boxf 3,mesy+3,171,mesy+101
		color
		mesy += 105
	}

	if hidelog_ = 0{
		pos 0,mesy
		mesy += 70
	} else {
		pos -175,mesy
	}
	mesbox logmessage,175,70,0
	logid = stat

	if availablecap = 0{
		objenable sscapbid,0
		objenable seqcapbid,0
		objenable yabumibid,0
	} else {
		objenable sscapbid,1
		objenable seqcapbid,1
		objenable yabumibid,1
	}
	
	if manualpos {
		objenable sscapbid,1
	}
	
	width 175,mesy
	
return

*windowsSizeChange
	if mainWindowSmall {
		mainWindowSmall = 0
		if hidelog = 0:hidelog_ = 0
	} else {
		mainWindowSmall = 1
		if hidelog = 0:hidelog_ = 1
	}
	if mode = 0:gosub *ssmode
	if (mode = 1 | mode = 2):gosub *list
return

//リストモード――――――――――――――――――――――――――――――――――
*list
	
	mxy(0) = 0,0
	mxy_(0) = 0,0
	cliflag = 0
	mesy = 31

	gsel 0,2
	clrobj 1
	syscolor 15:boxf:color
	
	pos 2,mesy
	mes "キャプチャ座標"
		
	objsize 80,25
	pos 95,mesy-3
	button gosub sscapmes,*sssetting
	sscapiid = stat
	mesy+=25
	
	objsize 175,20
	
	if listmode = 0{
	
		width 175
		
		pos 5,mesy
		chkbox "指輪も範囲に入れる",yubiwa:mesy+=20
		yubiwacid = stat
		yubiwach = objinfo_hwnd(yubiwacid)
		
		pos 5,mesy
		chkbox "所属艦隊も範囲に入れる",kantai:mesy+=20
		kantaicid = stat
		kantaich = objinfo_hwnd(kantaicid)
		
		pos 5,mesy
		chkbox "ページ数も範囲に入れる",page:mesy+=20
		pagecid = stat
		pagech = objinfo_hwnd(pagecid)
		pos 5,mesy
		
		chkbox "レベルと艦名のみにする",lvname:mesy+=20
		lvnamecid = stat
		lvnamech = objinfo_hwnd(lvnamecid)
	
		if lvname = 1{
			objenable yubiwacid,0
			objenable kantaicid,0
			objenable pagecid,0
		} else {
			objenable yubiwacid,1
			objenable kantaicid,1
			objenable pagecid,1
		}
		
	}
	if listmode = 1{
	
		width 350
		
		mesy2 = 5
	
		pos 5,mesy+1
		mes "自動追加の方法":mesy+=20
	
		objsize 70,20
		
		pos 7,mesy
		chkbox "縦3横2",tuika1
		tuika1cid = stat
		tuika1ch = objinfo_hwnd(tuika1cid)
		SetWindowLong tuika1ch, -16, $50000009 | $20000
		pos 80,mesy
		chkbox "縦2横3",tuika4
		tuika4cid = stat
		tuika4ch = objinfo_hwnd(tuika4cid)
		sendmsg tuika4ch, $F4, $9
		mesy+=20
		
		pos 7,mesy
		chkbox "縦6横1",tuika3
		tuika3cid = stat
		tuika3ch = objinfo_hwnd(tuika3cid)
		sendmsg tuika3ch, $F4, $9
		pos 80,mesy
		chkbox "縦1横6",tuika2
		tuika2cid = stat
		tuika2ch = objinfo_hwnd(tuika2cid)
		sendmsg tuika2ch, $F4, $9
		mesy+=20
	
		objsize 175,20
	
		pos 5,mesy+1
		mes "キャプチャ範囲":mesy+=18
	
		pos 7,mesy
		chkbox "装備とステータスと艦娘",sosuka:mesy+=20
		sosukacid = stat
		sosukach = objinfo_hwnd(sosukacid)
		SetWindowLong sosukach, -16, $50000009 | $20000
	
		pos 7,mesy
		chkbox "装備とステータス",sosu:mesy+=20
		sosucid = stat
		sosuch = objinfo_hwnd(sosucid)
		sendmsg sosuch, $F4, $9
	
		pos 7,mesy
		chkbox "装備と艦娘の画像",soka:mesy+=21
		sokacid = stat
		sokach = objinfo_hwnd(sokacid)
		sendmsg sokach, $F4, $9
	
		pos 5,mesy
		chkbox "艦隊区切り線を追加",fleetPunctuation:mesy+=20
		fleetPunctuationCId = stat
		fleetPunctuationCh = objinfo_hwnd(fleetPunctuationCId)
		pos 5,mesy
		chkbox "順番に番号を入れる",addOrderNum:mesy+=20
		addOrderNumCId = stat	
	
		window2x = 175
	
		objsize 70,22
		
		pos window2x+5,mesy2+1
		mes "艦隊名":mesy2+=20
		
		pos window2x+7,mesy2+1
		chkbox "第一艦隊",enableFirst
		pos window2x+80,mesy2
		input FirstFleetName,95,22:mesy2+=22

		pos window2x+7,mesy2+1
		chkbox "第二艦隊",enableSecond
		pos window2x+80,mesy2
		input SecondFleetName,95,22:mesy2+=22

		pos window2x+7,mesy2+1
		chkbox "第三艦隊",enableThread
		pos window2x+80,mesy2
		input ThreadFleetName,95,22:mesy2+=22

		pos window2x+7,mesy2+1
		chkbox "第四艦隊",enableFourth
		pos window2x+80,mesy2
		input FourthFleetName,95,22:mesy2+=25

		pos window2x+5,mesy2+1
		mes "艦隊名文字色":mesy2+=20
	
		drawColorPos = mesy2
		pos window2x+7,mesy2
		button gosub "第一艦隊",*colorCange1:mesy2+=22

		pos window2x+7,mesy2
		button gosub "第二艦隊",*colorCange2:mesy2+=22

		pos window2x+7,mesy2
		button gosub "第三艦隊",*colorCange3:mesy2+=22
		
		pos window2x+7,mesy2
		button gosub "第四艦隊",*colorCange4:mesy2+=22
	
		gosub *drawColor
		

	}

	if listmode = 2{
	
		width  175
		
		objsize 175,20
		
		pos 7,mesy
		chkbox "直接連結",directLink:mesy+=20
		directLinkCId = stat
		directLinkCh = objinfo_hwnd(directLinkCId)
		SetWindowLong directLinkCh, -16, $50000009 | $20000

		pos 7,mesy
		chkbox "基地航空隊",airStation:mesy+=20
		airStationCId = stat
		airStationCh = objinfo_hwnd(airStationCId)
		sendmsg airStationCh, $F4, $9


	}

	syscolor 16
	line 3,mesy,172,mesy
	color
	mesy+=3
	
	objsize 175,20
	
	pos 7,mesy
	chkbox "ツイートウィンドウ表示",enabletweetwindow:mesy+=20
	enabletweetwindowcid = stat
	enabletweetwindowch = objinfo_hwnd(enabletweetwindowcid)
			
	objsize 150-(((yabumiautoupload = 1) & (availableyabumi = 1))*25),30
	pos 0,mesy
	button gosub "作成",*make
	
	objsize 25,30
	pos 150-(((yabumiautoupload = 1) & (availableyabumi = 1))*25),mesy
	button gosub "開",*listsavefopen
	fopenbid = stat
	
	if ((yabumiautoupload = 1) & (availableyabumi = 1)) {
		pos 150,mesy
	} else {
		pos 500,mesy
	}	
	
	button gosub "UP",*yabumiupload
	yabumibid = stat
	
	mesy+=30

	pos 0,mesy
	mesbox logmessage,175,70,0 :mesy+=70
	logid = stat
	
	width ,mesy-(70*hidelog_)
	
	if IsWindowVisible(hwnd1) = 0{
		gsel 1,1
	} else {
		gsel 1,0
	}
	title modemes(listmode+1)+"モード - 表示させたいセルにD&D"
	width scrsize(0,listmode),scrsize(1,listmode)
	
	gosub *draw
	
return
	
//オプション――――――――――――――――――――――――――――――――――
*option

	gsel 1,-1
	gsel 0,1+frontrow
	clrobj 1
	syscolor 15:boxf:color
	
	mesy = 30
	pos 4,mesy
	mes "readmeをよくお読み下さい":mesy+=20
	
	objsize 350,20
	pos 5,mesy
	chkbox "提督名を隠すのではなくカットする",namecut:mesy+=20
	pos 5,mesy
	chkbox "常にメインウィンドウを最前列表示",frontrow:mesy+=20
	pos 5,mesy
	chkbox "ログを非表示",hidelog:mesy+=20
	pos 5,mesy
	chkbox "yabumiに自動でアップロードする",yabumiautoupload:mesy+=20
	pos 5,mesy
	chkbox "艦これが等倍でなくも取得できるようにする(手動のみ)",othergame:mesy+=20
	pos 5,mesy
	chkbox "キャプチャのプレビューを有効にする",dispcap:mesy+=20
	pos 5,mesy
	chkbox "取得座標を手動で入力できるようにする",manualpos:mesy+=20
	pos 5,mesy
	chkbox "画像の右下に艦これ一覧めいかーの名前を入れる",senden:mesy+=20
	pos 5,mesy
	chkbox "起動時に自動でバージョンチェックを行う",autoVersionCheck:mesy+=20
	pos 5,mesy
	chkbox "ホットキーでSSをキャプチャする",hotkeycap:mesy+=20

	objsize 50,20
	pos 30,mesy
	chkbox "Alt",hotkeycapalt
	pos 80,mesy
	chkbox "Ctrl",hotkeycapctrl
	pos 130,mesy
	chkbox "Shift",hotkeycapshift
	pos 180,mesy
	chkbox "Win",hotkeycapwin
	pos 240,mesy
	input hotkeycapchar,20,20,1
	pos 265,mesy
	mes "キー":mesy+=20
	
	objsize 350,20
	pos 5,mesy
	chkbox "＊一覧モードでSSを自動追加する",autoaddcaptcha:mesy+=20
	pos 5,mesy
	;chkbox "＊自動追加の場合はSSを保存しない",autoaddnonsave:mesy+=20
	pos 5,mesy
	chkbox "jpgで保存する",jpgsave:mesy+=20
	
	pos 5,mesy
	mes "jpg保存品質"
	pos 155,mesy
	mes "1〜100(大きいほど高画質)"
	pos 100,mesy
	input jpgquality,50,20,3:mesy+=20
	jpgqualityiid = stat
	GetWindowLong objinfo(jpgqualityiid,2), -16
	SetWindowLong objinfo(jpgqualityiid,2), -16, stat | $2
	
	pos 5,mesy
	chkbox "連続キャプチャを無効にする",disableseqcap:mesy+=20

	pos 5,mesy
	mes "連続キャプチャの間隔"
	pos 205,mesy
	mes "x0.1秒 (0.1〜99.9秒)"
	pos 150,mesy
	input timersec,50,20,3:mesy+=20
	timerseciid = stat
	GetWindowLong objinfo(timerseciid,2), -16
	SetWindowLong objinfo(timerseciid,2), -16, stat | $2
	
	pos 5,mesy+2
	mes "Twitter連携"
	objsize 250,25
	pos 100,mesy
	button gosub "認証情報をリセット",*twireset
	mesy+=25
	
	objsize 350,20
	pos 5,mesy
	chkbox "Twitterにはjpgで投稿する",twijpg:mesy+=20

	objsize 150,20
	pos 14,mesy
	mes "一覧保存先"
	pos 200,mesy
	button gosub "一覧保存先変更",*listsavedialog:mesy+=20
	pos 0,mesy
	mesbox listsavedir,350,45,5,259:mesy+=45
	listsdid = stat
	
	pos 14,mesy
	mes "SS保存先"
	pos 200,mesy
	button gosub "SS保存先変更",*sssavedialog:mesy+=20
	pos 0,mesy
	mesbox sssavedir,350,45,5,259:mesy+=45
	sssdid = stat
	
	pos 14,mesy
	mes "連続撮影保存先"
	pos 200,mesy
	button gosub "連続撮影保存先変更",*intervalshotsavedialog:mesy+=20
	pos 0,mesy
	mesbox intervalShotDir,350,45,5,259:mesy+=45
	intervalShotDirsdid = stat
	
	
	pos 14,mesy
	mes "YabumiUploader.exeの場所"
	pos 200,mesy
	button gosub "変更",*yabumiuploaderdialog:mesy+=20
	pos 0,mesy
	mesbox yabumidir,350,45,5,259:mesy+=45
	yabumiid = stat
	
	pos 0,mesy
	objsize 350,20
	button gosub "Amazonほしい物リスト",*Amazon:mesy+=20
	//http://www.amazon.co.jp/wishlist/181BGDSFXZC6M/?sort=priority
	
	width 350,mesy

return

*Amazon
	dialog "Amazonのほしい物リストだよ！\nぜひポチって支援してね！！！",2,"でちでち"
	if stat = 6:exec "http://www.amazon.co.jp/wishlist/181BGDSFXZC6M/?sort=priority",16
return


//SSキャプチャセッティング――――――――――――――――――――――――――――――――――
*sssetting
	
	gsel 0
	objenable comboxid,0
	objenable sscapiid,0
	if mode = 0{
		objenable sscapbid,0
		objenable seqcapbid,0
		if (yabumiautoupload = 1) & (availableyabumi = 1){
			objenable yabumibid,0
		}
	}

	gsel 10
	redraw 0
	BitBlt hdc,0,0, disinfo(2), disinfo(3), hdcScreen,  disinfo(0), disinfo(1),SRCCOPY
	redraw 1

	if autosearch = 0 {
		gsel 11,-1
		pos 0,0
		gcopy 10,0,0,disinfo(2),disinfo(3)
		color 128,128,128
		repeat (disinfo(3) / 120)+1
			ycnt = cnt
			repeat (disinfo(2) / 500)+2
				pos (cnt*600)+(ycnt*70)-800,ycnt*200
				mes "艦これ一覧めいかー　座標取得中"
			loop
		loop
		color 255,255,255
		boxf abs(disinfo(0)),abs(disinfo(1)),abs(disinfo(0))+970,abs(disinfo(1))+45
		color
		pos abs(disinfo(0))+5,abs(disinfo(1))+2
		mes "艦これの画面を覆うようにドラッグしてください Escキーでキャンセル"

		getkancollewindowposmanual 10,sscap,9,11,17
	
	} else {
		gsel 0
		logmessage = "座標自動取得中です"
		objprm logid,logmessage:await 

		getkancollewindowposauto2 10,sscap,20
		if stat = -1{
			if availablecap {
				dialog "自動座標取得に失敗しました\n前回の座標を残しますか？",2,"確認"
				if stat = 7{
					sscap(0) = 0,0,0,0
					availablecap = 0
				}
			}
				
				
		}
	}
	
	sscapwh(0) = sscap(2)-sscap(0),sscap(3)-sscap(1)
	luposx = sscap(0)
	luposy = sscap(1)
	rdposx = sscap(2)
	rdposy = sscap(3)
	
	gsel 0
	if manualpos & (mode = 0){
		objprm luposxiid,luposx
		objprm luposyiid,luposy
		objprm rdposxiid,rdposx
		objprm rdposyiid,rdposy
	}
	
	gsel 0,1+frontrow
	objenable comboxid,1
	objenable sscapiid,1
	
	if ( (sscapwh(0) = 800) & (sscapwh(1) = 480) ) | ( (othergame=1) & (autosearch=0) & (sscapwh(0) >= 99) & (sscapwh(1) >= 59) ){
		sscapmes = strf("%d,%d",sscap(0),sscap(1))
		objprm sscapiid,sscapmes
		
		if mode = 0{
			objenable sscapbid,1
			objenable seqcapbid,1
			objenable yabumibid,1
		}
		
		logmessage = "座標の取得に成功しました"
		objprm logid,logmessage
		availablecap = 1
	
		if autoSearch:autoSearchFailureCount = 0
		
	} else {
		sscapmes = "取得する"
		objprm sscapiid,sscapmes
		
		if mode = 0{
			objenable sscapbid,0
			objenable seqcapbid,0
			objenable yabumibid,0
		}
		
		logmessage = "座標を取得できません\n詳細はreadmeをお読みください"
		objprm logid,logmessage
		availablecap = 0
	
		if autoSearch{
			autoSearchFailureCount++
			if autoSearchFailureCount = 4{
				dialog "自動取得に連続して失敗しました\n手動取得に切り替えもう一度座標取得しますか？",2,""
				if stat = 7:autoSearchFailureCount = 0
			}
		}
		
	}
	
	if mode = 0 {
		if manualpos {
			objenable sscapbid,1
		}
		
		if (sscapwh(0) = 800) & (sscapwh(1) = 480){
			objenable hidenamebid,1
		} else {
			objenable hidenamebid,0
		}
	}
	
	if hotkeyset = 1 & availablecap = 1 {
		gsel 0,0
		title "*"+SOFTNAME
	} else {
		gsel 0,0
		title SOFTNAME
	}

	if autoSearch {
		if autoSearchFailureCount = 4{
			autoSearchFailureCount = 0
			autoSearch = 0
			objprm autoSearchCId,autoSearch
			gosub *sssetting
		}
	}

return

//SSキャプチャ――――――――――――――――――――――――――――――――――
*sscaptcha
	oncmd 0

	nidPush
	gsel 0
	
	//暫定数値直接入力
	sscap(0) = luposx
	sscap(1) = luposy
	sscap(2) = rdposx
	sscap(3) = rdposy
	sscapwh(0) = sscap(2)-sscap(0),sscap(3)-sscap(1)
	sscapmes = strf("%d,%d",sscap(0),sscap(1))
	if mode = 0{
		objprm sscapiid,sscapmes
	}
	//
	if (sscapwh(0) < 50) | (sscapwh(1) < 30){

		logmessage = "取得範囲が小さすぎます"
		if mode != modeOptionNum{
			objprm logid,logmessage
		}
		nidPop
		oncmd 1
		return
	}
		

	buffer 5,sscapwh(0),sscapwh(1)

	redraw 0
	BitBlt hdc, 0, 0, sscapwh(0), sscapwh(1), hdcScreen, sscap(0), sscap(1),SRCCOPY
	redraw 1

	if (homeport(5,20) = 1) & (hidename = 1) & (sscapwh(0) = 800) & (sscapwh(1) = 480) {

		pos 113,0
		gcopy 7,0,0,163,24
		pos 395,10
		gcopy 7,0,24,100,16

		if (namecut = 1) {
			buffer 5,800,450
			redraw 0
			BitBlt hdc, 0, 0, 800, 480,  hdcScreen, sscap(0), sscap(1)+30,SRCCOPY
			redraw 1
		}
	
	}
	
	gsel 5
	savename = strf("kancolle_%04d%02d%02d-%02d%02d%02d%03d%s",gettime(0),gettime(1),gettime(3),gettime(4),gettime(5),gettime(6),gettime(7),savekind(jpgsave))
	
	if seqcapf = 0{
		savenamepath = sssavedir +"\\"+ savename
		gdiimagesave sssavedir +"\\"+ savename,jpgquality
	} else {
		savenamepath = intervalShotDir+"\\"+intervalsavedir+"\\" +savename
		gdiimagesave intervalShotDir+"\\"+intervalsavedir+"\\" +savename,jpgquality
	}
	
	gsel 0
	
	if mode != modeOptionNum{
		if kmexist(savenamepath) <= 0{
			logmessage = savename+"の保存に失敗しました"
		} else {
			logmessage = savename+"を保存しました"
			if dispcap & (mode = 0){
				pos 0,dispcapy
				gzoom 175,105,5,0,0,sscapwh(0),sscapwh(1),1
			}
			
			if (seqcapf = 0) {
				i = 99
				repeat 99
					PicHistory(i) = PicHistory(i-1)
					i--
				loop
				PicHistory(0) = savenamepath 
				gosub *TweetWindow
			}
			
		}
		objprm logid,logmessage
	}

	nidPop
	oncmd 1
return


*listdrag

	oncmd 0

	if ginfo(24) = 1 {
	
		gsel 1
		dragdata = 0
		repeat
			stick sti,256
			if sti = 256{
				//mxy はドラッグを話した時点の座標
				//mxy_ はドラッグ開始の座標
				mxy(0) = limit(kmmousex/cellsize(0,listmode),0,cellnum(0,listmode)) ,limit(kmmousey/cellsize(1,listmode),0,cellnum(1,listmode))
				if (data(mxy(0),mxy(1),listmode)) >= 100 & cliflag = 0{				
					cliflag = 1
					mxy_(0) = mxy(0),mxy(1)
					mindexxy(0) = (mousex-(mxy_(0)*(cellsize(0,listmode)-1))),(mousey-(mxy_(1)*(cellsize(1,listmode)-1)))				
					gsel 4
					pos 0,0
					gcopy 1,0,0,maxscrwh(0),maxscrwh(1)
					gsel 3
					pos 0,0
					gzoom cellsize(0,listmode),cellsize(1,listmode),data(mxy_(0),mxy_(1),listmode),dd(0,listmode),dd(1,listmode),dd(2,listmode),dd(3,listmode),1
					gsel 1,0
				}
			
			}
		
			if cliflag = 1{
				redraw 0
				pos 0,0
				gcopy 4,0,0,maxscrwh(0),maxscrwh(1)
				gmode 3,cellsize(0,listmode),cellsize(1,listmode),100
				pos mousex-mindexxy(0),mousey-mindexxy(1)
				gcopy 3,0,0,cellsize(0,listmode),cellsize(1,listmode)
				gmode 0
				redraw 1
			}
			
			if sti = 0 & cliflag = 1{
				if (kmmousex < 0) | (kmmousex > scrsize(0,listmode)) | (kmmousey < 0) | (kmmousey > scrsize(1,listmode)){
					//マウス座標がウィンドウ外のとき
					if data(mxy_(0),mxy_(1),listmode) >= 100{
						bc( data(mxy_(0),mxy_(1),listmode) - 100 ) = 0
						data(mxy_(0),mxy_(1),listmode) = 0
						gsel 0
						logmessage = "セル("+mxy_(0)+","+mxy_(1)+")を削除しました"
						objprm logid,logmessage
						gsel 1		
					}
				} else {
					temp = data(mxy_(0),mxy_(1),listmode) 
					data(mxy_(0),mxy_(1),listmode) = data(mxy(0),mxy(1),listmode)
					data(mxy(0),mxy(1),listmode) = temp
					gsel 0
					logmessage = "セル("+mxy_(0)+","+mxy_(1)+")をセル("+mxy(0)+","+mxy(1)+")に移動しました"
					objprm logid,logmessage
					gsel 1
				}
		
				mxy(0) = 0,0
				mxy_(0) = 0,0
				temp = 0
				cliflag = 0
				gosub *draw
			}
		
			if sti = 0 & cliflag = 0{
				break
			}
	
			await 16
		loop
	}
	

	oncmd 1
	
return

//ドロップ処理――――――――――――――――――――――――――――――――――
*OnDropFiles
	oncmd 0
	if (ginfo(24) = 0) | (ginfo(24) = 1){
		nidPush
		dropwindowid = ginfo(24)
		hdrop = wParam
		sdim dropfile,260
		DragQueryFile hdrop, 0, varptr(dropfile), 260
		DragFinish hdrop
		if peek(dropfile,0) = '"' : getstr dropfile, dropfile, 1, '"'
		
		if ImgF_GetFormat(dropfile) = 0{
			nidPop
			return
		}
	
		addfilename = dropfile
		gosub *addcaptcha
		nidPop
	}
	if ginfo(24) = 19{
		nidPush
	
		hdrop = wParam
		sdim dropfile,260
		DragQueryFile hdrop, 0, varptr(dropfile), 260
		DragFinish hdrop
		if peek(dropfile,0) = '"' : getstr dropfile, dropfile, 1, '"'
		
		if ImgF_GetFormat(dropfile) = 0{
			nidPop
			return
		}

		i = 99
		repeat 99
			PicHistory(i) = PicHistory(i-1)
			i--
		loop
		PicHistory(0) = dropfile
	
		gosub *tweetWindow
	
		nidPop
	
	}
	oncmd 1
return

*addcaptcha
	
	oncmd 0
	gsel 1,0
	
	if (dropf = 1) & (dropwindowid = 1){
		xcnt = mousex/cellsize(0,listmode) 
		ycnt = mousey/cellsize(1,listmode)
		dmxy(0) = xcnt, ycnt
		if data(xcnt,ycnt,listmode) >= 100{
			dmxy(0) = xcnt, ycnt
			bc(data(xcnt, ycnt,listmode)-100) = 0
		}
	}
	if (autoaddcaptcha = 1){

		repeat
			if listmode = 0 {
				ycnt = cnt\(cellnum(1,listmode)+1)
				xcnt = cnt/(cellnum(1,listmode)+1)
			} 
			if listmode = 1{
				if tuika1{
					xcnt = (((cnt\(6*(cellnum(0,listmode)+1)/2))/6)*2)+(cnt\2)
					ycnt = ((cnt/((cellnum(0,listmode)+1)*3))*3)+((cnt\6)/2)
				}
				if tuika4{
					xcnt = (((cnt\(6*(cellnum(0,listmode)+1)/3))/6)*3)+(cnt\3)
					ycnt = ((cnt/((cellnum(0,listmode)+1)*2))*2)+((cnt\6)/3)
				}
				if tuika2 {
					xcnt = cnt\6
					ycnt = cnt/6
				}
				if tuika3{
					xcnt = cnt/6
					ycnt = cnt\6
				}
			}
			if listmode = 2 {
				ycnt = cnt\(cellnum(1,listmode)+1)
				xcnt = cnt/(cellnum(1,listmode)+1)
			} 
			xcnt = limit(xcnt,0,cellnum(0,listmode))
			ycnt = limit(ycnt,0,cellnum(1,listmode))
			if data(xcnt,ycnt,listmode) = 0{
				dmxy(0) = xcnt, ycnt
				break
			}
			if (xcnt=cellnum(0,listmode)) & (ycnt=cellnum(1,listmode)){
				dmxy(0) = xcnt, ycnt
				bc(data(xcnt, ycnt,listmode)-100) = 0
				break
			}
		loop
		
	}

	repeat (CELLWMAX*CELLHMAX*modeNum)
		if bc(cnt) = 0{
			bc(cnt) = 1
			bufid = cnt +100
			break
		}
	loop
	
	data(dmxy(0),dmxy(1),listmode) = bufid
	ImgF_GetPicSize addfilename,picx,picy

	if (picx != 480) & (picy != 450){
		buffer bufid,limit(picx,800,picx),limit(picy,480,picy)
		ImgF_PicloadEx addfilename,1
		gzoom 800,480,bufid,0,0,picx,picy,1
	} else {
		if picy = 480{
			buffer bufid,800,480
			pos 0,0
			ImgF_PicloadEx addfilename,1

		}
		if picy = 450{
			buffer bufid,800,480
			pos 0,30
			ImgF_PicloadEx addfilename,1
		}
	}

	gsel 1
	gosub *draw
	
	gsel 0
	logmessage = strf("%sをセル(%d,%d)に読み込みました",getpath(addfilename,11),dmxy(0),dmxy(1))
	objprm logid,logmessage
	
	gsel 1

	addfilename = ""
	hotkeyaddf = 0
	oncmd 1
return

//描画――――――――――――――――――――――――――――――――――
*draw
	
	gsel 1,0
	redraw 0
	color 255,255,255
	boxf
	color 150,150,150
	
	pos int(0.33333334*scrsize(0,listmode)),20
	gzoom int(0.66666667*scrsize(0,listmode)),int(0.15625*scrsize(0,listmode)),16,0,0,640,150
	
	repeat cellnum(0,listmode),1
		line  cnt*cellsize(0,listmode),-1,cnt*cellsize(0,listmode),maxscrwh(1)+1
	loop
	repeat cellnum(1,listmode),1
		line  -1,cnt*cellsize(1,listmode),maxscrwh(0)+1,cnt*cellsize(1,listmode)
	loop
	
	repeat cellnum(1,listmode)+1
		ycnt = cnt
		repeat cellnum(0,listmode)+1
			if data(cnt,ycnt,listmode) >= 100{
				pos cnt*cellsize(0,listmode),ycnt*cellsize(1,listmode)
				gzoom cellsize(0,listmode),cellsize(1,listmode),data(cnt,ycnt,listmode),dd(0,listmode),dd(1,listmode),dd(2,listmode),dd(3,listmode),1
			}
		loop
	loop
	
	if (mode = 2) & fleetPunctuation{
		color 32,32,192
		if tuika1 {
			boxf  2*cellsize(0,listmode)-1, -1, 2*cellsize(0,listmode)+1, maxscrwh(1)+1
			boxf  4*cellsize(0,listmode)-1, -1, 4*cellsize(0,listmode)+1, maxscrwh(1)+1
			boxf  -1, cellsize(1,listmode)*3-1, maxscrwh(0)+1, cellsize(1,listmode)*3+1
		}
		if tuika2 {
			boxf  -1, cellsize(1,listmode)-1, maxscrwh(0)+1, cellsize(1,listmode)+1
			boxf  -1, cellsize(1,listmode)*2-1, maxscrwh(0)+1, cellsize(1,listmode)*2+1
			boxf  -1, cellsize(1,listmode)*3-1, maxscrwh(0)+1, cellsize(1,listmode)*3+1
			boxf  -1, cellsize(1,listmode)*4-1, maxscrwh(0)+1, cellsize(1,listmode)*4+1
			boxf  -1, cellsize(1,listmode)*5-1, maxscrwh(0)+1, cellsize(1,listmode)*5+1
			
		}
		if tuika3 {
			boxf  cellsize(0,listmode)-1, -1, cellsize(0,listmode)+1, maxscrwh(1)+1
			boxf  cellsize(0,listmode)*2-1, -1, cellsize(0,listmode)*2+1, maxscrwh(1)+1
			boxf  cellsize(0,listmode)*3-1, -1, cellsize(0,listmode)*3+1, maxscrwh(1)+1
			boxf  cellsize(0,listmode)*4-1, -1, cellsize(0,listmode)*4+1, maxscrwh(1)+1
			boxf  cellsize(0,listmode)*5-1, -1, cellsize(0,listmode)*5+1, maxscrwh(1)+1
		}
	
		if tuika4 {
	
			boxf  3*cellsize(0,listmode)-1, -1, 3*cellsize(0,listmode)+1, maxscrwh(1)+1
			boxf  -1, cellsize(1,listmode)*2-1, maxscrwh(0)+1, cellsize(1,listmode)*2+1
			boxf  -1, cellsize(1,listmode)*4-1, maxscrwh(0)+1, cellsize(1,listmode)*4+1

		}
		
		color 0,0,0
		
	}
	
	redraw 1
return

*seqcap
	
	if seqcapf = 0{
		SetTimer hwnd0,timerID,100*timersec,0
		seqcapmes = "停止"
		objprm seqcapBId,seqcapmes
		objenable comboxid,0
		objenable sscapBId,0
		objenable sscapiid,0
		objenable yabumiBId,0
		seqcapf = 1
	
		chdir intervalShotDir
		intervalsavedir =  strf("IntervalShot%04d%02d%02d-%02d%02d%02d",gettime(0),gettime(1),gettime(3),gettime(4),gettime(5),gettime(6))
		if PathIsDirectory(intervalShotDir+"\\"+intervalsavedir) = 0 {
			mkdir intervalsavedir
		}
		chdir currentdir
		
		gosub *sscaptcha
	} else {
		KillTimer hwnd0,timerID
		seqcapmes = "開始"
		objprm seqcapBId,seqcapmes
		objenable comboxid,1
		objenable sscapBId,1
		objenable sscapiid,1
		objenable yabumiBId,1
		seqcapf = 0
	}
	
return

*yabumiupload
	
	if mode = 0 {	
		gosub *sscaptcha
		exec yabumidir+" \""+(sssavedir +"\\"+ savename)+"\""
	}
	if mode != 0{
		gosub *make
		if savename != ""{
			exec yabumidir+" \""+(listsavedir +"\\"+ savename)+"\""
		}
	}
return

//ホットキー――――――――――――――――――――――――――――――――――
*hotkey
	oncmd 0
	nidpush
	
	if (wparam = HOTKEYID) & (availablecap = 1){

		gosub *sscaptcha
		if ((mode != 0)&(mode != modeOptionNum)) {
			if autoaddcaptcha{
				addfilename = sssavedir +"\\"+ savename
				gosub *addcaptcha
			}
		}
		await 16
	}
	
	nidpop
	oncmd 1
return

*windowsize
	oncmd 0

	dupptr left,   lparam,    4
	dupptr top,    lparam+4,  4
	dupptr right,  lparam+8,  4
	dupptr bottom, lparam+12, 4

	switch wparam
	case 1
	case 2
	case 7
	case 8
		bottom = top + round(AspectRatio(listmode) * (right - left - framesx)) + framesy
		swbreak
	case 3
	case 5
	case 6
		right = left + round(1.0/AspectRatio(listmode) * (bottom - top - framesy)) + framesx
		swbreak
	case 4
		left = right - round(1.0/AspectRatio(listmode) * (bottom - top - framesy)) - framesx
		swbreak
	swend

	scrsize(0,listmode) = ginfo_winx ,round(AspectRatio(listmode)*ginfo_winx)
	cellsize(0,listmode) = round(1.0*scrsize(0,listmode) / (cellnum(0,listmode)+1)) ,round(1.0*scrsize(1,listmode) / (cellnum(1,listmode)+1))
	
	gosub *draw
	
	oncmd 1
return 1

	
//設定――――――――――――――――――――――――――――――――――
*listsavedialog
	
	BrowseFolder "一覧の保存先",(dir_exe)
	if stat = 1{
		listsavedir = refstr
		objprm listsdid,listsavedir
	}
	
return

*sssavedialog
	
	BrowseFolder "SSキャプチャの保存先",(dir_exe+"\\ScreenShots")
	if stat = 1{
		sssavedir = refstr
		objprm sssdid,sssavedir
	}

return

*intervalshotsavedialog
	
	BrowseFolder "連続撮影の保存先",(dir_exe+"\\ScreenShots")
	if stat = 1{
		intervalShotDir = refstr
		objprm intervalShotDirsdid,intervalShotDir
	}

return

*yabumiuploaderdialog
	
	dialog "exe",16,"YabumiUpLoader"
	if stat = 1{
		yabumidir = refstr
		objprm yabumiid,yabumidir
	}

return

*listsavefopen
	exec listsavedir,16
return

*sssavefopen
	exec sssavedir,16
return

*hotkeycapsetting

	if (hotkeycap = 1) & (hotkeystr != hotkeystr_temp) {
		UnregisterHotKey hwnd0,HOTKEYID
		RegisterHotKey hwnd0,HOTKEYID,(MOD_ALT*hotkeycapalt)|(MOD_CONTROL*hotkeycapctrl)|(MOD_SHIFT*hotkeycapshift)|(MOD_WIN*hotkeycapwin),hotkeycode
		hotkeystr_temp = hotkeystr
		if stat = 0{
			logmessage = "ホットキーの設定に失敗しました"
			objprm logid,logmessage
			hotkeyset = 0
		} else {
			logmessage = "ホットキーを設定しました"
			objprm logid,logmessage
			hotkeyset = 1
		}
	}
	
	if (hotkeycap = 0) & (hotkeyset = 1){
		UnregisterHotKey hwnd0,HOTKEYID
		logmessage = "ホットキーを解除しました"
		objprm logid,logmessage
		hotkeyset = 0
		hotkeystr_temp = ""
	}

	if hotkeyset = 1 & availablecap = 1 {
		gsel 0,0
		title "*"+SOFTNAME
	} else {
		gsel 0,0
		title SOFTNAME
	}
	
return


*disinfoinit
	
	disinfo(0) = GetSystemMetrics(76)
	disinfo(1) = GetSystemMetrics(77)
	disinfo(2) = GetSystemMetrics(78)
	disinfo(3) = GetSystemMetrics(79)
	
	init_makelist disinfo
	hdcScreen = CreateDC("DISPLAY", NULL, NULL, NULL)

return


*en_d
	
	if wparam = 1{
		mode = 0
		gosub *ssmode
		objprm comboxid,0
		return
	}
	
	if wparam = 19{
		gsel 19,-1
		enabletweetwindow = 0
		gsel 0
		if mode != modeOptionNum{
			objprm enabletweetwindowcid,0
		}
		return
	}

	DeleteDC hdcScreen
	
	DragAcceptFiles hwnd1, 0
	DragAcceptFiles hwnd0, 0
	DragAcceptFiles hwnd19, 0

	if seqcapf = 1{
		KillTimer hwnd0,timerID
	}

	if hotkeyset = 1 {
		UnregisterHotKey hwnd0,HOTKEYID
	}
	
	gosub *dataCheck
	gosub *iniSave
		
	end


#include "SubSource_iniFileOperation.hsp"
#include "SubSource_TwitterOperation.hsp"
#include "SubSource_DrawString.hsp"
#include "SubSource_make.hsp"
#include "SubSource_VersionCheck.hsp"